plugins {
    id 'java-library'
    id 'jacoco'
    id 'jvm-test-suite'
    id 'maven-publish'
    id "com.diffplug.spotless" version "6.25.0"
    id "io.freefair.lombok" version "8.4"
    id 'me.qoomon.git-versioning' version "6.4.2"
}

group 'com.github.Gtomika'
//git tag will be used as the version
version '0.0.0'

gitVersioning.apply {
    refs {
        //if there is a tag like '0.1.2' pointing to current commit, it will be used as version
        considerTagsOnBranches = true
        tag('(?<version>.*)') {
            version = '${ref.version}'
        }
        //otherwise, the branch name will be used to create a SNAPSHOT, such as 'master-SNAPSHOT'
        branch('.+') {
            version = '${ref}-SNAPSHOT'
        }
    }
}

ext {
    slf4jVersion = "1.7.25"
    classGraphVersion = "4.8.165"
    jgraphtVersion = "1.5.2"
    snakeYamlVersion = "2.2"
    junitJupiterVersion = "5.8.2"
    mockitoVersion = "5.8.0"
    log4jVersion = "2.17.2"
    tframeworkTestVersion = "0.0.3"
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "io.github.classgraph:classgraph:${classGraphVersion}"
    implementation "org.jgrapht:jgrapht-core:${jgraphtVersion}"
    implementation "org.yaml:snakeyaml:${snakeYamlVersion}"
}

testing {
    suites {
        //common test configuration that applies to all test suites
        configureEach {
            useJUnitJupiter()
            dependencies {
                implementation project()

                // testing frameworks
                implementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
                runtimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
                implementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
                runtimeOnly "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
                implementation "org.mockito:mockito-core:${mockitoVersion}"
                implementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"

                //logging during tests
                implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
                implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
                implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
            }
        }

        test {
            testType = TestSuiteType.UNIT_TEST
            dependencies {
            }
            targets {
                all {
                    testTask.configure {
                        // report + coverage check is always generated after tests run
                        finalizedBy("jacocoTestReport")
                    }
                }
            }
        }

        integrationTest(JvmTestSuite) {
            testType = TestSuiteType.INTEGRATION_TEST
            dependencies {
                implementation "com.github.Gtomika:tframework-test:${tframeworkTestVersion}"
                implementation "io.github.classgraph:classgraph:${classGraphVersion}"
            }
        }

    }
}

//spotless check configuration
spotless {
    format 'misc', {
        // define the files to apply `misc` to
        target '*.gradle', '*.md', '.gitignore'
        // define the steps to apply to those files
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
    java {
        importOrder()
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
        licenseHeader '/* Licensed under Apache-2.0 $YEAR. */'
    }
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('jacoco-report-html')
    }
    finalizedBy jacocoTestCoverageVerification //coverage is checked after report is generated
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.75
            }
        }
    }
}
